$system
Copyright Richard E. James III, 1981.
C Basic Complex arithmetic for Fortran.

      subroutine %C_ARI ( n, ar, ai, br, bi )
      real ai,ar, bi,br
c Input: two complex numbers;
c Output: one complex number, put into ar,ai.
      real q,s, cr

      goto (111,222,333,444),n

111   continue
*---------------------------------------------------------ADD
      ar = ar+br
      ai = ai+bi
      RETURN

222   continue
*---------------------------------------------------------SUB
      ar = ar-br
      ai = ai-bi
      RETURN

333   continue
*---------------------------------------------------------MUL
      cr = ar
      ar = cr*br - ai*bi
      ai = cr*bi + ai*br
      RETURN

444   continue
*---------------------------------------------------------DIV
C The algorithm given here avoids spurious overflows
C   and most underflows, plus it is fairly fast.
      cr = ar
      IF (abs(br).gt.abs(bi)) THEN
          q = bi/br
          s = br + bi*q
          ar = (cr+ai*q)/s
          ai = (ai-cr*q)/s
          RETURN
      ELSE
          q = br/bi
          s = bi + br*q
          ar = (ai+cr*q)/s
          ai = (q*ai-cr)/s
          RETURN
      ENDIF

      END

C Complex raised to a power.
*---------------------------------------------- (complex ** complex)
      complex function %_CUPC ( cb, ce )
*                      ------
      complex cb,ce
      integer ie
      integer ierror
      logical %c_cck
      complex %_cexp,%_clog
      complex %_CUPI

C Complex exponention routines:
C         %_CUPC for C**C
C         %_CUPI for C**I
C     where C=complex, I=4-byte integer

      IF (%c_cck(ierror,cb)) THEN
          IF (%c_cck(ierror,ce)) THEN
              %_CUPC = %_cexp(ce * %_clog(cb))
          ENDIF
      ENDIF
      IF (ierror.ne.0) call %_cerr(%_cupc,'C ** C',ierror)
      RETURN
*---------------------------------------------- (complex ** integer)
      entry %_CUPI ( cb, ie )
C           ------
      IF (%c_cck(ierror,cb)) THEN
          IF (ie.eq.-1) THEN
              %_CUPI = 1 / cb
          ELSEIF (ie.eq.0) THEN
              IF (real(cb).eq.0 .and. aimag(cb).eq.0) THEN
                  ierror = 3
              ELSE
                  %_CUPI = (0.0, 0.0)
              ENDIF
          ELSEIF (ie.eq.1) THEN
              %_CUPI = cb
          ELSEIF (ie.eq.2) THEN
              %_CUPI = cb*cb
          ELSE
              %_CUPI = %_cexp(ie * %_clog(cb))
          ENDIF
      ENDIF
      IF (ierror.ne.0) call %_cerr(%_cupi,'C ** I',ierror)
      RETURN

      END
C Complex Fortran functions.
*-----------------------------------------------------------CABS
      real    function %_CABS
     +                        ( c )
      complex c
      integer ierror
      real mn,mx, q
      logical %c_cck

      IF (%c_cck(ierror,c)) THEN
          mn = min(abs(real(c)), abs(aimag(c)))
          mx = max(abs(real(c)), abs(aimag(c)))
          IF (mx.eq.0) THEN
              %_CABS = 0
          ELSE
C This algorithm is good to about 1.4 ulp.
              q = mn/mx
              %_CABS = mx + mn*q / (1.+sqrt(1.+q*q))
          ENDIF
      ELSE
          call %_cerr(mn, 'CABS  ',ierror)
          %_cabs = mn
      ENDIF
      RETURN
      END
*-----------------------------------------------------------CCOS
      complex function %_CCOS
     +                        ( c )
      complex c
      logical %c_cck

      IF (%c_cck(ierror,c)) THEN
          %_CCOS = cmplx( cos(real(c)) * cosh(aimag(c)),
     ,                  - sin(real(c)) * sinh(aimag(c)) )
      ELSE
          call %_cerr(%_ccos, 'CCOS  ',ierror)
      ENDIF
      RETURN
      END
*-----------------------------------------------------------CEXP
      complex function %_CEXP
     +                        ( c )
      complex c
      integer ierror
      real e
      logical %c_cck

      IF (%c_cck(ierror,c)) THEN
          e = exp(real(c))
          %_CEXP = cmplx( e * cos(aimag(c)), e * sin(aimag(c)) )
      ELSE
          call %_cerr(%_cexp, 'CEXP  ',ierror)
      ENDIF
      RETURN
      END
*-----------------------------------------------------------CLOG
      complex function %_CLOG
     +                        ( c )
      complex c, ce
      integer ierror,domerr
      real mn,mx,u
      logical %c_cck
      parameter (domerr=3)

      IF (%c_cck(ierror,c)) THEN
          mn = min(abs(real(c)),abs(aimag(c))) 
          mx = max(abs(real(c)),abs(aimag(c)))
          IF (.5 .lt. mx .and. mx .lt. 1.22) THEN
C             Work hard on getting an accurate real part to the answer
C              when the argument is near the unit circle.
              u = .5 * log(1. + ((mx-1)*(mx+1) + mn*mn))
          ELSEIF (mx.eq.0) THEN
              ierror = domerr
          ELSE
              u = mn/mx
              u = log(mx) + .5 * log(1. + u*u)
          ENDIF
      ENDIF
      IF (ierror.eq.0) THEN
          %_CLOG = cmplx ( u, atan2(aimag(c), real(c)) )
      ELSE
          call %_cerr(ce, 'CLOG  ',ierror)
          %_clog = ce
      ENDIF
      RETURN
      END
*-----------------------------------------------------------CSIN
      complex function %_CSIN
     +                        ( c )
      complex c
      integer ierror
      logical %c_cck

      IF (%c_cck(ierror,c)) THEN
          %_CSIN = cmplx( sin(real(c)) * cosh(aimag(c)),
     ,                    cos(real(c)) * sinh(aimag(c)) )
      ELSE
          call %_cerr(%_csin, 'CSIN  ',ierror)
      ENDIF
      RETURN
      END
*-----------------------------------------------------------CSQRT
      complex function %_CSQT
     +                        ( c )
      complex c
      integer ierror
      real t,s,tt, %_cabs
      logical %c_cck

      IF (%c_cck(ierror,c)) THEN
          t = %_cabs(c)
          IF (t.eq.0) THEN
              %_CSQT = c
              RETURN
          ENDIF
          s = sqrt(0.5 * (t + abs(real(c))))
          t = aimag(c) / (s+s)
          IF (real(c).lt.0) THEN
              tt = t
              t = sign(s,t)
              s = abs(tt)
          ENDIF
      ENDIF
      IF (ierror.eq.0) THEN
          %_CSQT = cmplx(s,t)
      ELSE
          call %_cerr(%_csqt, 'CSQRT ',ierror)
      ENDIF
      RETURN
      END

