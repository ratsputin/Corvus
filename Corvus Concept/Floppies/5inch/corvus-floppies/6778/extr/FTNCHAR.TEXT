; File: ftnchar.text
; Date: 13-Dec-82

        ident   ftnchar
        
        global  %fc_ass,%fc_tmp,%fc_sub,%fc_sub1,%fc_fin
        global  %fc_cmp,%_ichar,%_char,%_len,%_index
        extern  %_ferror

;
; %fc_ass - Assign some characters to a FORTRAN character variable
;
; Parameters: ST.L - Address to put next character
;             ST.W - Number of bytes left unasigned
;             ST.L - Address of characters to copy
;             ST.W - Number of characters to copy
;
; Result:     ST.L - Address of next free byte in destination
;             ST.W - Number of bytes left unassigned
;
; This routine clobbers: D0,D1,A0,A1,A2
;

%fc_ass
        move.l  (sp)+,a2        ; Pop return address
        move.w  (sp)+,d1        ; Length of source
        move.l  (sp)+,a1        ; Address of source
        move.w  (sp)+,d0        ; Room in destination
        move.l  (sp)+,a0        ; Address of destination
        cmp.w   d0,d1           ; Will this overflow dest?
        ble.s   as_room         ; No. Do it all.
        move.w  d0,d1           ; Yes. Only copy what fits
as_room sub.w   d1,d0           ; D0 = Final room left
        bra.s   as_test         ; D1 = count to copy
as_loop move.b  (a1)+,(a0)+     ; Copy the next byte
as_test subq.w  #1,d1           ; Any more to copy?
        bpl.s   as_loop         ; Yes.
        move.l  a0,-(sp)        ; No. Push results
        move.w  d0,-(sp)        ;
        jmp     (a2)            ; and return
        
;
; %fc_tmp - Assign some characters to a FORTRAN character temporary
;           This routine assumes there is adaquate room to copy
;
; Parameters: ST.L - Address of first character of temporary
;             ST.W - Number of bytes used in temporary
;             ST.L - Address of characters to copy
;             ST.W - Number of characters to copy. Must be > 0.
;
; Result:     ST.L - Address of first character of temporary
;             ST.W - Number of bytes used in temporary
;
; This routine clobbers: D0,D1,A0,A1,A2
;

%fc_tmp
        move.l  (sp)+,a2        ; Pop return address
        move.w  (sp)+,d1        ; Length of source
        move.l  (sp)+,a1        ; Address of source
        move.w  (sp)+,d0        ; Length of destination
        move.l  (sp),a0         ; Address of temporary
        adda.w  d0,a0           ; First free hole
        add.w   d1,d0           ; D0 = Final size
        move.w  d0,-(sp)        ; Push final length
        subq.w  #1,d1           ; Get ready for DBRA
tm_loop move.b  (a1)+,(a0)+     ; Copy the next byte
        dbra    d1,tm_loop      ; Any more to copy?
        jmp     (a2)            ; No. Return
        
;
; %fc_sub - Compute and verify a FORTRAN character substring
;           For substrings as: VVV(EEE:EEE) or VVV(:EEE)
;
; Parameters: ST.L - Address of main string
;             ST.W - Size of main string
;             ST.W - Beginning substring position
;             ST.W - Ending substring position
;
; Result:     ST.L - Address of substring
;             ST.W - Size of substring
;
; This routine preserves all registers.
;

%fc_sub
        movem.l d0-d2/a0-a1,-(sp)
        move.l  20(sp),a1       ; Pop return address
        move.w  24(sp),d2       ; End of substring
        move.w  26(sp),d1       ; Start of substring
        ble.s   as_err          ; Error if min <= 0
        move.w  28(sp),d0       ; Length of main string
        move.l  30(sp),a0       ; Address of string
        cmp.w   d0,d2           ; Is max too big?
        bgt.s   as_err          ; Yes.
        sub.w   d1,d2           ; No. Is min > max?
        blt.s   as_err          ; Yes.
        addq.w  #1,d2           ; No. Set D2 to size of substring
        lea     -1(a0,d1.w),a0  ; Compute substring address
        move.l  a0,30(sp)       ; and store it
        move.w  d2,28(sp)       ; Store substring length
        move.l  a1,24(sp)       ; Store return address
        movem.l (sp)+,d0-d2/a0-a1
        addq.w  #4,sp
        rts
as_err  move.l  a1,-(sp)        ; Pass return address for ..
        move.w  #707,-(sp)      ; .. FORTRAN run time error #707
        jsr     %_ferror

;
; %fc_sub1 - Compute and verify a FORTRAN character substring
;            Upper limit on substring is taken as string size
;            For substrings as: VVV(EEE:) or VVV(:)
;
; Parameters: ST.L - Address of main string
;             ST.W - Size of main string
;             ST.W - Beginning substring position
;
; Result:     ST.L - Address of substring
;             ST.W - Size of substring
;
; This routine preserves all registers.
;

%fc_sub1
        movem.l d0-d1/a0-a1,-(sp)
        move.l  16(sp),a1       ; Pop return address
        move.w  20(sp),d1       ; Start of substring
        ble.s   as_err          ; Error if min <= 0
        move.w  22(sp),d0       ; Length of main string
        move.l  24(sp),a0       ; Address of string
        sub.w   d1,d0           ; No. Is min > length?
        blt.s   as_err          ; Yes.
        addq.w  #1,d0           ; No. Set D0 to size of substring
        lea     -1(a0,d1.w),a0  ; Compute substring address
        move.l  a0,24(sp)       ; and store it
        move.w  d0,22(sp)       ; Store substring length
        move.l  a1,18(sp)       ; Store return address
        movem.l (sp)+,d0-d1/a0-a1
        addq.w  #2,sp
        rts
        
;
; %fc_fin - Finish character assign and blank fill
;
; Parameters: ST.L - Address to put next character
;             ST.W - Number of bytes left unasigned
;             ST.L - Address of characters to copy
;             ST.W - Number of characters to copy
;
; This routine clobbers: D0,D1,A0,A1,A2
;

%fc_fin
        move.l  (sp)+,a2        ; Pop return address
        move.w  (sp)+,d1        ; Length of source
        move.l  (sp)+,a1        ; Address of source
        move.w  (sp)+,d0        ; Room in destination
        move.l  (sp)+,a0        ; Address of destination
        cmp.w   d0,d1           ; Will this overflow dest?
        ble.s   fi_room         ; No. Do it all.
        move.w  d0,d1           ; Yes. Only copy what fits
fi_room sub.w   d1,d0           ; D0 = Final room left
        bra.s   fi_test         ; D1 = count to copy
fi_loop move.b  (a1)+,(a0)+     ; Copy the next byte
fi_test subq.w  #1,d1           ; Any more to copy?
        bpl.s   fi_loop         ; Yes.
                                ; No. D0 = count, A0 = address
        subq.w  #1,d0           ; Get ready for DBRA
        blt.s   fi_done         ; Done if count <= zero
        moveq   #' ',d1         ; Get a fast blank
fi_blnk move.b  d1,(a0)+        ; Blank the next byte
        dbra    d0,fi_blnk      ; More to fill?
fi_done jmp     (a2)
        
;
; %fc_cmp - Compare two FORTRAN character values
;
; Parameters: ST.L - Address of left value
;             ST.W - Size of left value
;             ST.L - Address of rignt value
;             ST.W - Size of right value
;
; Assumes: The length of both args is > 0
;
; Result:     CC   - Result of comparison
;
; This routine preserves all registers.
;

%fc_cmp
        movem.l d0-d1/a0-a2,-(sp)
        lea     24(sp),a2       ; (A2) - arguments
        move.w  (a2)+,d1        ;  D1  = right length
        move.l  (a2)+,a1        ; (A1) = right value
        move.w  (a2)+,d0        ;  D0  = left length
        move.l  (a2)+,a0        ; (A0) = left value
        move.l  20(sp),-(a2)    ; Set up return address
        cmp.w   d0,d1           ; See which is longer
        beq.s   cm_eq           ; Neither.
        blt.s   cm_ll           ; Left arg is
;
; Right arg is longer
;
        sub.w   d0,d1           ; D1 = extra size of right arg
        subq.w  #1,d0           ; Set up DBNE instruction
cm_rlp  cmpm.b  (a1)+,(a0)+     ; Are they equal?
        dbne    d0,cm_rlp       ; Yes. So far.
        bne.s   cm_done         ; If not equal, we're done.
        moveq   #' ',d0         ; Get a fast blank
        subq.w  #1,d1           ; Set up DBNE instruction
cm_rlp2 cmp.b   (a1)+,d0        ; See if left all blanks
        dbne    d1,cm_rlp2      ;
        bra.s   cm_done         ;
;
; Left arg is longer
;
cm_ll   sub.w   d1,d0           ; D0 = extra size of left arg
        subq.w  #1,d1           ; Set up DBNE instruction
cm_llp  cmpm.b  (a1)+,(a0)+     ; Are they equal?
        dbne    d1,cm_llp       ; Yes. So far.
        bne.s   cm_done         ; If not equal, we're done.
        subq.w  #1,d0           ; Set up DBNE instruction
cm_llp2 cmpi.b  #' ',(a0)+      ; See if right all blanks
        dbne    d0,cm_llp2      ;
        bra.s   cm_done         ;
;
; Neither arg is longer
;
cm_eq   subq.w  #1,d0           ; Set up DBNE instruction
cm_elp  cmpm.b  (a1)+,(a0)+     ; Are they equal?
        dbne    d0,cm_elp       ; Yes. So far.
cm_done movem.l (sp)+,d0-d1/a0-a2
        adda    #12,sp
        rts
        
;
; %_ichar - Convert character into integer
;
; Parameters: ST.L - Address of character string
;             ST.W - Length of character string
;
; Returns:    ST.L - Integer value of character
;
; All registers are preserved.
;

%_ichar
        movem.l d0/a0,-(sp)
        clr.l   d0              ; Place to put result
        move.l  14(sp),a0       ; (A0) = string
        move.b  (a0),d0         ; Fetch first character
        move.l  d0,14(sp)       ; Store result
        move.l  8(sp),10(sp)    ; Set up return address
        movem.l (sp)+,d0/a0
        addq.l  #2,sp
        rts
        
;
; %_char - Convert integer into character
;
; Parameters: ST.L - Address to put character
;             ST.B - Integer
;
; Returns:    ST.L - Address of character
;             ST.W - Length (=1)
;
; All registers are preserved.
;

%_char
        move.l  a0,-(sp)
        move.l  10(sp),a0       ; Get address of temp
        move.b  8(sp),(a0)      ; Put character there
        move.w  #1,8(sp)        ; Fill in string length
        move.l  (sp)+,a0
        rts
        
;
; %_len - Return the length of a string
;
; Parameters: ST.L - Room for function result
;             ST.L - Address of character string
;             ST.W - Length of character string
;
; Returns:    ST.L - Integer length
;
; All registers are preserved.
;

%_len
        move.l  d0,-(sp)
        move.w  8(sp),d0        ; D0 = length
        ext.l   d0
        move.l  d0,14(sp)       ; Store result
        move.l  4(sp),10(sp)    ; Set up return address
        move.l  (sp)+,d0
        addq.l  #6,sp
        rts
        
;
; %_index - Return the position of a substring in a string
;
; Parameters: ST.L - Room for function result  sp+36
;             ST.L - Address of main string    sp+32
;             ST.W - Length of main string     sp+30
;             ST.L - Address of substring      sp+26
;             ST.W - Length of substring       sp+24
;
; Returns:    ST.L - Integer length
;
; All registers are preserved.
;

%_index
        movem.l d0-d2/a1-a2,-(sp)
        clr.l   d0              ; D0 = position of match
        bra.s   in_test         ;
in_loop addq.l  #1,d0           ; D0 = new starting position
        move.l  32(sp),a1       ; A1 = place to search main string
        move.l  26(sp),a2       ; A2 = Address of substring
        addq.l  #1,32(sp)       ; Bump main string address
        subq.w  #1,30(sp)       ; and main string length
        subq.w  #1,d2           ;
in_lop2 cmpm.b  (a1)+,(a2)+     ; Are characters the same?
        dbne    d2,in_lop2      ;
        beq.s   in_done         ; If equal, return answer
in_test move.w  30(sp),d1       ; D1 = Length of main string
        move.w  24(sp),d2       ; D2 = Length of substring
        cmp.w   d2,d1           ; Is there room for a match?
        bge.s   in_loop         ; Yes. Try next position
        clr.l   d0              ; Set result to 0 = No-Match
in_done move.l  d0,36(sp)       ; Store result
        move.l  20(sp),32(sp)   ; Set up return address
        movem.l (sp)+,d0-d2/a1-a2
        adda.w  #12,sp
        rts
        
        end

